plugins {
	id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'edu.sc.seis.launch4j' version '2.5.4'
}

apply plugin: 'java'
apply plugin: 'application'

apply from: 'private.gradle'

repositories {
	mavenLocal()	
	mavenCentral()
}

launch4j {
  mainClassName = 'de.rangun.joinpangaea.JoinPangaeaApp'
  icon = "${projectDir}/pangaea.icon"
}

application {
    mainClass = 'de.rangun.joinpangaea.JoinPangaeaApp'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(16))
    }
}

dependencies {
	implementation "com.google.code.gson:gson:${project.gson_version}"
	
	implementation("io.github.fragland:MineStat:${project.minestat_version}") {
		exclude(group: "junit")
		exclude module: "junit"
	}
	
	shadow("com.google.code.gson:gson:${project.gson_version}");
	shadow("io.github.fragland:MineStat:${project.minestat_version}")
}

processResources {
	inputs.property "apiKeySecret", CurseforgeAPIKey

	filesMatching("api.json") {
		expand "apiKey": CurseforgeAPIKey
	}
	
	exclude('config/worldedit')
	exclude('config/.puzzle_cache')
	exclude('config/physicsmod')
	exclude('config/thief')
	exclude('config/replaymod*')
	exclude('config/litematica/')
	exclude('config/minihud/')
	exclude('config/voxelmap.properties')
	exclude('config/atmosfera.json')
	exclude('config/charmonium.toml*')
	exclude('config/inspecio.json')
	exclude('config/panda_crossing.json')
	exclude('config/worldeditcui.config.json')
}

shadowJar {

	dependsOn createExe
	dependsOn distZip
	dependsOn distTar

    archiveClassifier.set('')
    
    relocate 'com.google.gson', 'de.rangun.joinpangaea.shadowed.com.google.gson'
    relocate 'me.dilley', 'de.rangun.joinpangaea.shadowed.me.dilley'

    exclude 'META-INF/**'
    
    minimize()
}

jar {
  manifest {
    attributes(
      'Main-Class': 'de.rangun.joinpangaea.JoinPangaeaApp'
    )
  }
}
